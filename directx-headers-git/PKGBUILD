#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Laurent Carlier <lordheavym@archlinux.org>
#Credits: Cyano Hao <c@cyano.cn>

pkgname=directx-headers-git
pkgdesc='DirectX headers for using D3D12 (git version)'
pkgver=1.613.1.r211.gaafb798
pkgrel=1
commit=7fc7184d25f76f0797438d404733a76251396c83
arch=(x86_64)
url='https://github.com/microsoft/DirectX-Headers'
license=(MIT)
makedepends=(meson git)
conflicts=(directx-headers)
provides=(directx-headers directx-headers-git)
options=(!lto) # LTO breaks mesa...
source=(git+https://github.com/microsoft/DirectX-Headers.git#commit="${commit}")

pkgver(){
  cd "${srcdir}"/DirectX-Headers
  #git describe --long --tags --abbrev=7 --exclude *preview --exclude *mesa* --exclude *-r1 --exclude *2b | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  echo $(sed -n 4p meson.build | sed 's/[^0-9]*//g').r$(git rev-list --count HEAD).g$(git rev-parse --short=7 HEAD) | sed 's/./&./1' | sed 's/./&./5'
}

build(){
# LTO breaks mesa...
export CFLAGS="$CFLAGS -fno-lto"
export CXXFLAGS="$CXXFLAGS -fno-lto"

  rm -rf "${srcdir}"/build

  local meson_options=(
    -D b_ndebug=true
    -D b_lto=false
    -D buildtype=plain
    --wrap-mode=nofallback
    -D prefix=/usr
    -D bindir=bin
    -D datadir=share
    -D includedir=include
    -D libdir=lib
    -D sysconfdir=/etc
    -D build-test=false
  )

  meson "${srcdir}"/DirectX-Headers "${srcdir}"/build "${meson_options[@]}"
  meson configure --no-pager "${srcdir}"/build
  meson compile -C "${srcdir}"/build
}

package(){
  DESTDIR="${pkgdir}" meson install -C "${srcdir}"/build

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/DirectX-Headers/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
