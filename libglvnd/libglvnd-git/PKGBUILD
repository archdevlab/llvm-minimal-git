#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Felix Yan <felixonmars@archlinux.org>
#Credits: Rafał Kozdrój <kozeid2@gmail.com>
#Credits: Laurent Carlier <lordheavym@gmail.com>

pkgname=libglvnd-git
pkgdesc='The GL Vendor-Neutral Dispatch library (git version)'
pkgver=1.7.0.r879.g606f662
pkgrel=1
commit=606f6627cf481ee6dcb32387edc010c502cdf38b
arch=(x86_64)
url='https://gitlab.freedesktop.org/glvnd/libglvnd'
license=(custom:BSD-like)
# libglvnd needs mesa for indirect rendering
depends=(libxext mesa opengl-driver)
makedepends=(libxext libx11 xorgproto python meson git)
provides=(libglvnd libgl libegl libgles)
provides+=(libEGL.so libGL.so libGLESv2.so libGLX.so libGLdispatch.so libOpenGL.so)
conflicts=(libglvnd)
source=(git+https://gitlab.freedesktop.org/glvnd/libglvnd.git#commit="${commit}"
        LICENSE)

pkgver(){
  cd "${srcdir}"/libglvnd
  #git describe --long --tags --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
  echo $(sed -n 27p meson.build | cut -c 13- | sed "s/'//g" | sed 's/,//g').r$(git rev-list --count HEAD).g$(git rev-parse --short=7 HEAD)
}

build() {
  rm -rf "${srcdir}"/build

  # Fix for build error
  sed -i "s/subdir('tests')/ /" "${srcdir}"/libglvnd/meson.build

  local meson_options=(
    -D b_ndebug=true
    -D b_lto=false
    -D buildtype=plain
    --wrap-mode=nofallback
    -D prefix=/usr
    -D bindir=bin
    -D datadir=share
    -D includedir=include
    -D libdir=lib
    -D sysconfdir=/etc
    -D asm=enabled
    -D x11=enabled
    -D egl=true
    -D glx=enabled
    -D gles1=true
    -D gles2=true
    -D tls=true
    -D dispatch-tls=true
    -D headers=true
    -D entrypoint-patching=enabled
  )

  meson "${srcdir}"/libglvnd "${srcdir}"/build "${meson_options[@]}"
  meson configure --no-pager "${srcdir}"/build
  meson compile -C "${srcdir}"/build
}

package_libglvnd-git(){
  DESTDIR="${pkgdir}" meson install -C "${srcdir}"/build

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP
            88cace72bc02c54c096ab825a55488a7a0fcc5ea73513a97fc16102fc07ea9b3)

# vim:set ts=8 sts=2 sw=2 et:
