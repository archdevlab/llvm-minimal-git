#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Jan de Groot <jgc@archlinux.org>
#Credits: Eric Engestrom <eric@engestrom.ch>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>

pkgname=lib32-libdrm-git
pkgdesc='Userspace interface to kernel DRM services (32-bit) (git version)'
pkgver=2.4.122.r7132.gad78bb5
pkgrel=1
commit=ad78bb591d02162d3b90890aa4d0a238b2a37cde
arch=(i686 x86_64)
url='http://dri.freedesktop.org/'
license=(custom)
depends=(lib32-libpciaccess libdrm)
makedepends=(meson git)
checkdepends=(lib32-cairo)
provides=(lib32-libdrm lib32-libdrm-git)
conflicts=(lib32-libdrm)
source=(git+https://gitlab.freedesktop.org/mesa/drm.git#commit=$"${commit}"
        COPYING)

pkgver(){
  cd "${srcdir}"/drm
  #git describe --long --tags --abbrev=7 | sed 's/^libdrm.//;s/\([^-]*-g\)/r\1/;s/-/./g'
  echo $(sed -n 24p meson.build | cut -c 13- | sed "s/'//g" | sed 's/,//g').r$(git rev-list --count HEAD).g$(git rev-parse --short=7 HEAD)
}

build() {
export CFLAGS+=" -m32"
export CXXFLAGS+=" -m32"
export LDFLAGS+=" -m32"
export ASFLAGS+=" --32"
export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"
export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  rm -rf "${srcdir}"/build

  local meson_options=(
    --cross-file lib32
    -D b_ndebug=true
    -D b_lto=false
    -D buildtype=plain
    --wrap-mode=nofallback
    -D prefix=/usr
    -D bindir=bin
    -D datadir=share
    -D includedir=include
    -D libdir=lib32
    -D sysconfdir=/etc
    -D intel=enabled
    -D radeon=enabled
    -D amdgpu=enabled
    -D nouveau=enabled
    -D vmwgfx=enabled
    -D omap=enabled
    -D exynos=enabled
    -D freedreno=enabled
    -D tegra=enabled
    -D vc4=enabled
    -D etnaviv=enabled
    -D cairo-tests=disabled
    -D man-pages=disabled
    -D valgrind=disabled
    -D freedreno-kgsl=true
    -D install-test-programs=true
    -D udev=true
    -D tests=true
  )

  meson "${srcdir}"/drm "${srcdir}"/build "${meson_options[@]}"
  meson configure --no-pager "${srcdir}"/build
  meson compile -C "${srcdir}"/build
}

package_lib32-libdrm-git(){
  DESTDIR="${pkgdir}" meson install -C "${srcdir}"/build

  rm -rf "${pkgdir}"/usr/{include,share,bin}

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"/
}

sha256sums=(SKIP
            9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93)

# vim:set ts=8 sts=2 sw=2 et:
