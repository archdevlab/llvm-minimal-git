#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Daniel Bermond <dbermond@archlinux.org>
#Credits: Bruno Pagani <archange@archlinux.org>
#Credits: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>

pkgname=lib32-spirv-llvm-translator-minimal-git
pkgdesc='Tool and a library for bi-directional translation between SPIR-V and LLVM IR (32-bit) (git version)'
url='https://github.com/KhronosGroup/SPIRV-LLVM-Translator'
pkgver=19.0.0.r2080.g277654e
_pkgver=19.0.0
pkgrel=1
# https://github.com/KhronosGroup/SPIRV-LLVM-Translator/issues/2432
# https://github.com/KhronosGroup/SPIRV-LLVM-Translator/pull/2433
# https://github.com/KhronosGroup/SPIRV-LLVM-Translator/pull/2433/commits
# https://github.com/KhronosGroup/SPIRV-LLVM-Translator/pull/2433/commits/e0a1508be91faffe433038fd5e50cc87d85d02d6
commit=277654e5b16a47acbd6bc14693995f92058ff514
arch=(i686 x86_64)
license=(custom)
depends=(lib32-llvm-libs-minimal-git lib32-spirv-tools-git)
makedepends=(git cmake llvm-minimal-git lib32-llvm-minimal-git spirv-headers-git)
conflicts=(lib32-spirv-llvm-translator)
provides=(lib32-spirv-llvm-translator)
provides+=(lib32-spirv-llvm-translator-minimal-git lib32-spirv-llvm-translator-git)
source=(git+https://github.com/KhronosGroup/SPIRV-LLVM-Translator.git#commit="${commit}")

pkgver(){
  cd "${srcdir}"/SPIRV-LLVM-Translator
  printf "${_pkgver}"."r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build(){
export CFLAGS+=" -m32"
export CXXFLAGS+=" -m32"
export LDFLAGS+=" -m32"
export ASFLAGS+=" --32"
export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"
export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  rm -rf "${srcdir}"/SPIRV-LLVM-Translator/.git/

  local cmake_args=(
      -G "Unix Makefiles"
      -D CMAKE_CXX_FLAGS:STRING=-m32
      -D CMAKE_C_FLAGS:STRING=-m32
      -D CMAKE_BUILD_TYPE=Release
      -D CMAKE_INSTALL_PREFIX=/usr
      -D CMAKE_INSTALL_BINDIR=bin
      -D CMAKE_INSTALL_LIBDIR=lib32
      -D CMAKE_INSTALL_INCLUDEDIR=include
      -D CMAKE_INSTALL_SYSCONFDIR=/etc
      -D CMAKE_INSTALL_DATADIR=share
      -D LLVM_LIBDIR_SUFFIX=32
      -D CMAKE_POSITION_INDEPENDENT_CODE=ON
      -D CMAKE_SKIP_RPATH=ON
      -D LLVM_EXTERNAL_SPIRV_HEADERS_SOURCE_DIR=/usr/include/spirv/
      -D LLVM_INCLUDE_TESTS=OFF
      -D BUILD_SHARED_LIBS=ON
      -D LLVM_CONFIG=/usr/bin/llvm-config32
    )

  cmake -B "${srcdir}"/build -S "${srcdir}"/SPIRV-LLVM-Translator "${cmake_args[@]}" -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build
}

package(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include}

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/SPIRV-LLVM-Translator/LICENSE.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
