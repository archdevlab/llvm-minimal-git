#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Daniel Bermond <dbermond@archlinux.org>
#Credits: Bruno Pagani <archange@archlinux.org>
#Credits: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>

pkgname=spirv-llvm-translator-minimal-git
pkgdesc='Tool and a library for bi-directional translation between SPIR-V and LLVM IR (git version)'
url='https://github.com/KhronosGroup/SPIRV-LLVM-Translator'
pkgver=19.0.0.r2083.gf7c340c
_pkgver=19.0.0
pkgrel=1
commit=f7c340c39e9f762ca7223242f2c6bc1476daab39
arch=(x86_64)
license=(custom)
depends=(llvm-libs-minimal-git spirv-tools-git)
makedepends=(git cmake llvm-minimal-git spirv-headers-git)
checkdepends=(python python-setuptools clang-minimal-git)
provides=(spirv-llvm-translator)
provides+=(spirv-llvm-translator-minimal-git spirv-llvm-translator-git)
conflicts=(spirv-llvm-translator)
source=(git+https://github.com/KhronosGroup/SPIRV-LLVM-Translator.git#commit="${commit}")

pkgver(){
  cd "${srcdir}"/SPIRV-LLVM-Translator
  printf "${_pkgver}"."r%s.g%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

build(){
  rm -rf "${srcdir}"/SPIRV-LLVM-Translator/.git/

  local cmake_args=(
      -G "Unix Makefiles"
      -D CMAKE_BUILD_TYPE=Release
      -D CMAKE_INSTALL_PREFIX=/usr
      -D CMAKE_INSTALL_BINDIR=bin
      -D CMAKE_INSTALL_LIBDIR=lib
      -D CMAKE_INSTALL_INCLUDEDIR=include
      -D CMAKE_INSTALL_SYSCONFDIR=/etc
      -D CMAKE_INSTALL_DATADIR=share
      -D CMAKE_POSITION_INDEPENDENT_CODE=ON
      -D CMAKE_SKIP_RPATH=ON
      -D LLVM_EXTERNAL_LIT=/usr/bin/lit
      -D LLVM_EXTERNAL_SPIRV_HEADERS_SOURCE_DIR=/usr/include/spirv/
      -D LLVM_INCLUDE_TESTS=ON
      -D BUILD_SHARED_LIBS=ON
      -D LLVM_CONFIG=/usr/bin/llvm-config
    )

  cmake -B "${srcdir}"/build -S "${srcdir}"/SPIRV-LLVM-Translator "${cmake_args[@]}" -Wno-dev

  make -j$(nproc) -C "${srcdir}"/build
}

check(){
  LD_LIBRARY_PATH="${srcdir}"/build/lib/SPIRV make -j$(nproc) -C "${srcdir}"/build test
}

package(){
  make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install

  install -Dm755 "${srcdir}"/build/tools/llvm-spirv/llvm-spirv -t "${pkgdir}"/usr/bin/

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/SPIRV-LLVM-Translator/LICENSE.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
