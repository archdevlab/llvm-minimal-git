#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Laurent Carlier <lordheavym@gmail.com>
#Credits: Lone_Wolf <lone_wolf@klaas-de-kat.nl>

pkgname=libclc-minimal-git
pkgdesc='Library requirements of the OpenCL C programming language (git version)'
url='https://libclc.llvm.org/'
pkgver=19.0.0.r496211.g396cdabe47f3
pkgrel=1
commit=326ba38a991250a8587a399a260b0f7af2c9166a
arch=(x86_64)
license=(MIT)
makedepends=(clang-minimal-git llvm-minimal-git cmake ninja python spirv-llvm-translator-minimal-git llvm-libs make git)
# https://aur.archlinux.org/packages/libclc-minimal-git#comment-967212
# https://github.com/llvm/llvm-project/issues/88626
makedepends+=(meson ninja)
conflicts=(libclc)
provides=(libclc)
provides+=(libclc-minimal-git libclc-git)
source=(git+https://github.com/llvm/llvm-project.git#commit="${commit}")

pkgver(){
  cd "${srcdir}"/llvm-project/

  # This will almost match the output of `llvm-config --version` when the
  # LLVM_APPEND_VC_REV cmake flag is turned on. The only difference is
  # dash being replaced with underscore because of Pacman requirements.
  local _pkgver=$(awk -F 'MAJOR |MINOR |PATCH |)' \
  'BEGIN { ORS="." ; i=0 } \
  /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
  END { print "\n" }' \
  cmake/Modules/LLVMVersion.cmake).r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)
  echo "${_pkgver}"
}

build(){
# https://aur.archlinux.org/cgit/aur.git/commit/?h=llvm-minimal-git&id=ae64860f523a57e2c6d0a9e65ed8af8c82d1ca73
# build aborts with FORTIFY_SOURCE=3 , see https://github.com/llvm/llvm-project/issues/85509
export CFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"
export CXXFLAGS="${CFLAGS/_FORTIFY_SOURCE=3/_FORTIFY_SOURCE=2}"

  rm -rf "${srcdir}"/llvm-project/.git/
  
  # https://aur.archlinux.org/packages/libclc-minimal-git#comment-967212
  # https://github.com/llvm/llvm-project/issues/88626
  local cmake_args=(
      -G Ninja
      #-G "Unix Makefiles"
      -D CMAKE_BUILD_TYPE=Release
      -D CMAKE_INSTALL_PREFIX=/usr
      -D CMAKE_INSTALL_BINDIR=bin
      -D CMAKE_INSTALL_LIBDIR=lib
      -D CMAKE_INSTALL_INCLUDEDIR=include
      -D CMAKE_INSTALL_SYSCONFDIR=/etc
      -D CMAKE_INSTALL_DATADIR=share
      -D LLVM_CONFIG=/usr/bin/llvm-config
    )

  cmake -B "${srcdir}"/build -S "${srcdir}"/llvm-project/libclc "${cmake_args[@]}" -Wno-dev

  # https://aur.archlinux.org/packages/libclc-minimal-git#comment-967212
  # https://github.com/llvm/llvm-project/issues/88626
  #make -j$(nproc) -C "${srcdir}"/build
  ninja -j$(nproc) -C "${srcdir}"/build
}

package(){
  # https://aur.archlinux.org/packages/libclc-minimal-git#comment-967212
  # https://github.com/llvm/llvm-project/issues/88626
  #make -j$(nproc) -C "${srcdir}"/build DESTDIR="${pkgdir}" install
  DESTDIR="${pkgdir}" ninja -j$(nproc) -C "${srcdir}"/build install

  # install license
  install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "${srcdir}"/llvm-project/libclc/LICENSE.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
  install -m644 "${srcdir}"/llvm-project/libclc/CREDITS.TXT "${pkgdir}"/usr/share/licenses/"${pkgname}"/CREDITS
}

sha256sums=(SKIP)

# vim:set ts=8 sts=2 sw=2 et:
